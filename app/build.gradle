buildscript {
    dependencies {
        classpath 'org.xtend:xtend-gradle-plugin:0.1.+'
    }
}

apply plugin: 'android'
apply plugin: 'xtend-android'
apply plugin: 'eclipse'

import org.gradle.plugins.ide.eclipse.model.*

eclipse.classpath.plusConfigurations += [configurations.compile]

eclipse.classpath.file.whenMerged { classpath ->
  classpath.entries += [new Container("org.eclipse.xtend.XTEND_CONTAINER"), new SourceFolder("src/main/java", "")]
  classpath.entries += ["ANDROID_FRAMEWORK", "LIBRARIES", "DEPENDENCIES"]
    .collect { new Container("com.android.ide.eclipse.adt.$it") }
}

eclipse.project.file.whenMerged { project ->
  project.natures += 'com.android.ide.eclipse.adt.AndroidNature'
  project.buildCommands += ['ResourceManagerBuilder', 'PreCompilerBuilder', 'ApkBuilder']
    .collect { new BuildCommand("com.android.ide.eclipse.adt.$it") }
}

android {
    compileSdkVersion 19
    buildToolsVersion "20.0.0"

    dependencies {
        compile fileTree(dir: 'libs', include: ['*.jar'])
        compile 'org.eclipse.xtend:org.eclipse.xtend.lib:2.6.+'
        compile 'com.github.tobykurien:xtendroid:0.10.+'
    }

    sourceSets {
      main {
        res {
          srcDir 'res'
        }
      }
    }

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}
